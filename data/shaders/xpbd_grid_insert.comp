#version 430 core

// Build spatial hash grid for particle collision broad-phase

layout(local_size_x = 256) in;

struct Particle {
    vec4 pos;       // xyz position, w = inverse mass
    vec4 pos_prev;  // previous position
    vec4 vel;       // xyz velocity, w = microbe_id
    vec4 data;      // x = particle_index, y = constraint_start, z = constraint_count
};

layout(std430, binding = 0) readonly buffer Particles { Particle particles[]; };
layout(std430, binding = 1) buffer GridHead { int head[]; };
layout(std430, binding = 2) buffer GridNext { int next[]; };

uniform vec2 u_bounds;
uniform float u_cell;
uniform ivec2 u_grid_dim;
uniform int u_particle_count;

void main() {
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= uint(u_particle_count)) return;

    vec3 pos = particles[idx].pos.xyz;

    int gx = int((pos.x + u_bounds.x) / u_cell);
    int gz = int((pos.z + u_bounds.y) / u_cell);
    gx = clamp(gx, 0, u_grid_dim.x - 1);
    gz = clamp(gz, 0, u_grid_dim.y - 1);

    int cell = gz * u_grid_dim.x + gx;

    // Atomic linked list insertion
    next[idx] = atomicExchange(head[cell], int(idx));
}
